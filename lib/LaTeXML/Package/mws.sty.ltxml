#!/usr/bin/perl -w
# -*- CPERL -*-
# /=====================================================================\ #
# |  mws.sty - Implementation for LaTeXML                               | #
# |                                                                     | #
# |=====================================================================| #
# | NOT Part of LaTeXML:                                                | #
# |  Public domain software, produced as part of work done by the       | #
# |  United States Government & not subject to copyright in the US.     | #
# |---------------------------------------------------------------------| #
# | Deyan Ginev <d.ginev@jacobs-university.de>                          | #
# | http://dlmf.nist.gov/LaTeXML/                                       | #
# \=====================================================================/ #

package LaTeXML::Package::Pool;
use strict;
use LaTeXML::Package;
use LaTeXML::Post::MathML;

#Experimental: MathWebSearch
# We want \qvar{X} to represent a "query variable" X for the MathWebSearch query.
DefConstructor('\qvar@construct Semiverbatim Optional','<ltx:XMTok name="qvar" meaning="qvar" color="red" ?#2(type="#2")>#1</ltx:XMTok>',requireMath=>1);

# And for authoring simplicity, we use ?X as an alias to \qvar{X}
AssignMathcode('?'=>0x8000, 'global');
DefMacro('?','\qvar');
DefMacro('\qvar Semiverbatim OptionalMatch::',sub{
  my ($gullet,$var,$type_declaration) = @_;
  # Allowing for an optional type argument:
  my $type;
  if (defined $type_declaration) {
    $type = $gullet->readArg(); }
  return Invocation(T_CS('\qvar@construct'),$var,$type); });

# attempt to implement mws:range
# this should take the lower and the upper bound
DefConstructor('\range{}{}',  "<ltx:XMApp color='red'>"
    ."<ltx:XMTok name='range' meaning='range' role='OPERATOR' color='red'/>"
    ."<ltx:XMArg color='red'>#1</ltx:XMArg>"
    ."<ltx:XMArg color='red'>#2</ltx:XMArg>"
    ."</ltx:XMApp>", requireMath => 1);

# this is a binding for relative ranges
# it takes a base value and a percentage and creates a range with base +-
# precentage%
DefMacro('\relrange{}{}', sub {
        my ($gullet, $val , $fac) = @_;
        my $num = ToString($val);
        my $factor = ToString($fac);
        my $diff = $factor/100 * ( $num || 1 ); # to do something usefull wenn $num = 0
        # i'll say thats enough precession
        my $low = sprintf "%.2f", $num - $diff;
        my $high = sprintf "%.2f", $num + $diff;
        return Invocation(T_CS('\range'), $low, $high);
    });

package LaTeXML::Post::MathML;
# Support for rewriting of qvar symbols during post-processing to MathML
DefMathML('Token:?:qvar', undef, sub { ['m:csymbol', { cd => 'mws', name => 'qvar', type=>$_[0]->getAttribute('type') }, $_[0]->textContent]; });

DefMathML('Apply:?:range',
    sub {
        my $low = $_[1]->textContent;
        my $high = $_[2]->textContent;
        pmml_parenthesize(pmml_mn("$low...$high"), '[', ']');
    }
    ,
    sub {
        my $low = $_[1]->textContent;
        my $high = $_[2]->textContent;
        my $attr = {
         cd => 'mws',
         name => 'range'
        };
        # check if there are values for high and low
        if(length($low)){
            $attr->{'low'} = $low;
        }
        if(length($high)){
            $attr->{'high'} = $high;
        }

        return ['m:csymbol', $attr, 'range'];
    });


1;
